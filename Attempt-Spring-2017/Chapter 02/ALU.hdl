// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	//Zero X
	Mux16(a=x, b=false, sel=zx,out=xZero);
	//Zero Y
	Mux16(a=y, b=false, sel=zy, out=yZero);

	//Not X
	Not16(in=xZero,out=xNot);
	Mux16(a=xZero,b=xNot,sel=nx,out=x2);	
	//Not Y
	Not16(in=yZero,out=yNot);
	Mux16(a=yZero,b=yNot,sel=ny,out=y2);

	//Compute f
	Add16(a=x2,b=y2,out=add);
	And16(a=x2,b=y2,out=and);
	
	Mux16(a=and,b=add,sel=f,out=functOut);
	
	Not16(in=functOut,out=functOut2);
	//Inc16(in=o2,out=o3);	
	Mux16(a=functOut,b=functOut2,sel=no,out[0..7]=out07, out[8..15]=out815, out[15]=ng1 ,out=out);

	//Set ng flag if the MSB is 1
	And(a=ng1,b=ng1,out=ng);
	
	//Set zr flag if any of the bits are 1
	Or8Way(in=out07,out=zero1);
	Or8Way(in=out815,out=zero2);
	Or(a=zero1,b=zero2,out=zr1);
	Not(in=zr1,out=zr);
	
}
